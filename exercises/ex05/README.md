[í™ˆ - RAP100](../../#exercises)

# ì—°ìŠµë¬¸ì œ 5: BO ê¸°ëŠ¥ ê°œì„  â€“ Validations

## ì†Œê°œ

ì´ì „ ì—°ìŠµì—ì„œëŠ” BO ì—”í‹°í‹° _Travel_ ì˜ ìƒˆ ì¸ìŠ¤í„´ìŠ¤ë¥¼ ìƒì„±í•˜ëŠ” ë™ì•ˆ `OverallStatus` í•„ë“œì˜ ì´ˆê¸°ê°’ì„ `Open` (`O`)ìœ¼ë¡œ ì„¤ì •í•˜ê¸° ìœ„í•œ determinationì„ ì •ì˜í•˜ê³  êµ¬í˜„í–ˆìŠµë‹ˆë‹¤( [ì—°ìŠµ 4](../ex04/README.md) ì°¸ì¡°).

ì´ë²ˆ ì—°ìŠµì—ì„œëŠ” ì†Œë¹„ìê°€ ì…ë ¥í•œ ê³ ê° IDê°€ ìœ íš¨í•œì§€, ì‹œì‘ì¼ì´ ë¯¸ë˜ì¸ì§€, ì¢…ë£Œì¼ ê°’ì´ ì‹œì‘ì¼ ì´í›„ì¸ì§€ ê°ê° í™•ì¸í•˜ê¸° ìœ„í•´ ë‘ ê°œì˜ ë°±ì—”ë“œ validation, `validateCustomer`ì™€ `validateDates`ë¥¼ ì •ì˜í•˜ê³  êµ¬í˜„í•  ê²ƒì…ë‹ˆë‹¤. ì´ëŸ¬í•œ validationì€ UIê°€ ì•„ë‹Œ ë°±ì—”ë“œì—ì„œë§Œ ìˆ˜í–‰ë˜ë©°, Fiori UI ë˜ëŠ” EML APIì™€ ê°™ì€ í˜¸ì¶œìì™€ ë…ë¦½ì ìœ¼ë¡œ íŠ¸ë¦¬ê±°ë©ë‹ˆë‹¤.

> â„¹ **Frontend validation & Backend validations**
> Validationì€ ë°ì´í„° ì¼ê´€ì„±ì„ ë³´ì¥í•˜ëŠ” ë° ì‚¬ìš©ë©ë‹ˆë‹¤.
> ì´ë¦„ì—ì„œ ì•Œ ìˆ˜ ìˆë“¯ì´, **frontend validations** ì€ UIì—ì„œ ìˆ˜í–‰ë©ë‹ˆë‹¤. ë” ë¹ ë¥¸ í”¼ë“œë°±ì„ ì œê³µí•˜ê³  ë¶ˆí•„ìš”í•œ ì™•ë³µ(roundtrip)ì„ í”¼í•˜ì—¬ ì‚¬ìš©ì ê²½í—˜ì„ ê°œì„ í•˜ëŠ” ë° ì‚¬ìš©ë©ë‹ˆë‹¤. RAP ì»¨í…ìŠ¤íŠ¸ì—ì„œ frontend validationsì€ CDS annotation ë˜ëŠ” UI ë¡œì§ì„ ì‚¬ìš©í•˜ì—¬ ì •ì˜ë©ë‹ˆë‹¤.
> ë°˜ë©´ì—, **backend validations** ì€ ë°±ì—”ë“œì—ì„œ ìˆ˜í–‰ë©ë‹ˆë‹¤. BO behavior definitionì— ì •ì˜ë˜ê³  ê°ê°ì˜ behavior poolì—ì„œ êµ¬í˜„ë©ë‹ˆë‹¤.
> Frontend validationsì€ RAP ì»¨í…ìŠ¤íŠ¸ì—ì„œ EML APIë¥¼ ì‚¬ìš©í•˜ëŠ” ë“± ì‰½ê²Œ ìš°íšŒí•  ìˆ˜ ìˆìŠµë‹ˆë‹¤. ë”°ë¼ì„œ **backend validationsì€ ë°ì´í„° ì¼ê´€ì„±ì„ ë³´ì¥í•˜ê¸° ìœ„í•´ í•„ìˆ˜** ì…ë‹ˆë‹¤.

- [5.1 - Validation `validateCustomer` ë° `validateDates` ì •ì˜](#ì—°ìŠµ-51-validation-validatecustomer-ë°-validatedates-ì •ì˜)
- [5.2 - Validation `validateCustomer` êµ¬í˜„](#ì—°ìŠµ-52-validation-validatecustomer-êµ¬í˜„)
- [5.3 - Validation `validateDates` êµ¬í˜„](#ì—°ìŠµ-53-validation-validatedates-êµ¬í˜„)
- [5.4 - í–¥ìƒëœ Travel ì•± ë¯¸ë¦¬ë³´ê¸° ë° í…ŒìŠ¤íŠ¸](#ì—°ìŠµ-54-í–¥ìƒëœ-travel-ì•±-ë¯¸ë¦¬ë³´ê¸°-ë°-í…ŒìŠ¤íŠ¸)
- [ìš”ì•½](#ìš”ì•½)

> **ì•Œë¦¼**: ì•„ë˜ ì—°ìŠµ ë‹¨ê³„ì—ì„œ ì ‘ë¯¸ì‚¬ ìë¦¬ í‘œì‹œì **`###`** ë¥¼ ì„ íƒí•˜ê±°ë‚˜ í• ë‹¹ëœ ê·¸ë£¹ IDë¡œ ë°”ê¾¸ëŠ” ê²ƒì„ ìŠì§€ ë§ˆì‹­ì‹œì˜¤.

### Validationsì— ëŒ€í•˜ì—¬

Validationì€ íŠ¸ë¦¬ê±° ì¡°ê±´ì— ë”°ë¼ ë¹„ì¦ˆë‹ˆìŠ¤ ì˜¤ë¸Œì íŠ¸ ì¸ìŠ¤í„´ìŠ¤ì˜ ì¼ê´€ì„±ì„ í™•ì¸í•˜ëŠ” ë¹„ì¦ˆë‹ˆìŠ¤ ì˜¤ë¸Œì íŠ¸ behaviorì˜ ì„ íƒì  ë¶€ë¶„ì…ë‹ˆë‹¤.

Validationì˜ íŠ¸ë¦¬ê±° ì¡°ê±´ì´ ì¶©ì¡±ë˜ë©´ ë¹„ì¦ˆë‹ˆìŠ¤ ì˜¤ë¸Œì íŠ¸ì˜ í”„ë ˆì„ì›Œí¬ì— ì˜í•´ ì•”ì‹œì ìœ¼ë¡œ í˜¸ì¶œë©ë‹ˆë‹¤. íŠ¸ë¦¬ê±° ì¡°ê±´ì€ `MODIFY` ì‘ì—… ë° ìˆ˜ì •ëœ í•„ë“œê°€ ë  ìˆ˜ ìˆìŠµë‹ˆë‹¤. íŠ¸ë¦¬ê±° ì¡°ê±´ì€ BO ëŸ°íƒ€ì„ ë™ì•ˆ ë¯¸ë¦¬ ì •ì˜ëœ ì‹œì ì¸ íŠ¸ë¦¬ê±° ì‹œê°„ì— í‰ê°€ë©ë‹ˆë‹¤. í˜¸ì¶œëœ validationì€ ì‹¤íŒ¨í•œ ì¸ìŠ¤í„´ìŠ¤ì˜ í‚¤ë¥¼ `FAILED` êµ¬ì¡°ì˜ í•´ë‹¹ í…Œì´ë¸”ì— ì „ë‹¬í•˜ì—¬ ì¼ê´€ì„± ì—†ëŠ” ì¸ìŠ¤í„´ìŠ¤ ë°ì´í„°ê°€ ì €ì¥ë˜ëŠ” ê²ƒì„ ê±°ë¶€í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤. ë˜í•œ, validationì€ ë©”ì‹œì§€ë¥¼ `REPORTED` êµ¬ì¡°ì˜ í•´ë‹¹ í…Œì´ë¸”ì— ì „ë‹¬í•˜ì—¬ ì†Œë¹„ìì—ê²Œ ë©”ì‹œì§€ë¥¼ ë°˜í™˜í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

> **ì¶”ê°€ ì •ë³´**: [Validations](https://help.sap.com/viewer/923180ddb98240829d935862025004d6/Cloud/en-US/171e26c36cca42699976887b4c8a83bf.html)

## ì—°ìŠµ 5.1: Validation `validateCustomer` ë° `validateDates` ì •ì˜
[^ë§¨ ìœ„ë¡œ](#)

> `validateCustomer`ì™€ `validateDates` validationì„ ì •ì˜í•©ë‹ˆë‹¤.

<details>
  <summary>ğŸ”µ í´ë¦­í•˜ì—¬ í¼ì¹˜ê¸°!</summary>

1.  behavior definition ![behaviordefinition](images/adt_bdef.png) **`ZRAP100_R_TRAVELTP_###`** ë¥¼ ì—½ë‹ˆë‹¤.

2.  **`CustomerID`**, **`BeginDate`** ë° **`EndDate`** í•„ë“œì—ëŠ” ë¹ˆ ê°’ì„ í—ˆìš©í•˜ì§€ ì•Šìœ¼ë¯€ë¡œ, ì•„ë˜ ìŠ¤í¬ë¦°ìƒ·ê³¼ ê°™ì´ determination ë’¤ì— ë‹¤ìŒ ì½”ë“œ ìŠ¤ë‹ˆí«ì„ ì¶”ê°€í•˜ì—¬ _mandatory_ í•„ë“œë¡œ ì§€ì •í•©ë‹ˆë‹¤.

    ```abap
     field ( mandatory )
     CustomerID,
     BeginDate,
     EndDate;
    ```

    ì†ŒìŠ¤ ì½”ë“œëŠ” ë‹¤ìŒê³¼ ê°™ì•„ì•¼ í•©ë‹ˆë‹¤:

    <!-- ![validation](images/new18a.png)    -->
    <img src="images/new18a.png" alt="validation" width="60%">

3.  **`validateCustomer`** ì™€ **`validateDates`** validationì„ ì •ì˜í•©ë‹ˆë‹¤.

    ì´ë¥¼ ìœ„í•´ ì•„ë˜ ìŠ¤í¬ë¦°ìƒ·ê³¼ ê°™ì´ determination ë’¤ì— ë‹¤ìŒ ì½”ë“œ ìŠ¤ë‹ˆí«ì„ ì¶”ê°€í•©ë‹ˆë‹¤.

    ```abap
    validation validateCustomer on save { create; field CustomerID; }
    validation validateDates on save { create; field BeginDate, EndDate; }
    ```

4.  draft ì¸ìŠ¤í„´ìŠ¤ê°€ validationì— ì˜í•´ í™•ì¸ë˜ê³  determinationì´ í™œì„±í™”ë˜ê¸° ì „ì— ì‹¤í–‰ë˜ë„ë¡ í•˜ë ¤ë©´, behavior definitionì—ì„œ **`draft determine action prepare`** ì— ì§€ì •í•´ì•¼ í•©ë‹ˆë‹¤.

    **`draft determine action Prepare;`** ì½”ë“œ ë¼ì¸ì„ ì•„ë˜ ìŠ¤í¬ë¦°ìƒ·ê³¼ ê°™ì´ ë‹¤ìŒ ì½”ë“œ ìŠ¤ë‹ˆí«ìœ¼ë¡œ ë°”ê¿‰ë‹ˆë‹¤.

    ```abap
    draft determine action Prepare
    {
    validation validateCustomer;
    validation validateDates;    }
    ```

    ì†ŒìŠ¤ ì½”ë“œëŠ” ë‹¤ìŒê³¼ ê°™ì•„ì•¼ í•©ë‹ˆë‹¤:

    <!-- ![validation](images/new18.png) -->
    <img src="images/new18.png" alt="validation" width="60%">

    ** ê°€ë‹¨í•œ ì„¤ëª…**:
    -   Validationsì€ í•­ìƒ ì €ì¥ ì¤‘ì— í˜¸ì¶œë˜ë©° `validateCustomer on save` í‚¤ì›Œë“œë¡œ ì§€ì •ë©ë‹ˆë‹¤.
    -   `validateCustomer`ëŠ” íŠ¸ë¦¬ê±° ì‘ì—…ì´ `create`ì´ê³  íŠ¸ë¦¬ê±° í•„ë“œê°€ `CustomerID`ì¸ validationì…ë‹ˆë‹¤.
    -   `validateDates`ëŠ” íŠ¸ë¦¬ê±° ì‘ì—…ì´ `create`ì´ê³  íŠ¸ë¦¬ê±° í•„ë“œê°€ `BeginDate`ì™€ `EndDate`ì¸ validationì…ë‹ˆë‹¤.

    **â„¹ íŒíŠ¸**:
    > BO ì—”í‹°í‹° ì¸ìŠ¤í„´ìŠ¤ì˜ ëª¨ë“  ë³€ê²½ ì‹œ validationì´ í˜¸ì¶œë˜ì–´ì•¼ í•˜ëŠ” ê²½ìš°, íŠ¸ë¦¬ê±° ì¡°ê±´ `create`ì™€ `update`ë¥¼ ì§€ì •í•´ì•¼ í•©ë‹ˆë‹¤: ì˜ˆ: `validation validateCustomer on save { create; update; }`

5.  ![save icon](images/adt_save.png) ì €ì¥í•˜ê³  ![activate icon](images/adt_activate.png) ë³€ê²½ì‚¬í•­ì„ í™œì„±í™”í•©ë‹ˆë‹¤.

6.  quick fixë¥¼ í†µí•´ _Travel_ BO ì—”í‹°í‹°ì˜ behavior poolì˜ ë¡œì»¬ í•¸ë“¤ëŸ¬ í´ë˜ìŠ¤ì— ì ì ˆí•œ **`FOR VALIDATE ON SAVE`** ë©”ì„œë“œë¥¼ ì¶”ê°€í•©ë‹ˆë‹¤.

    ì´ë¥¼ ìœ„í•´ validation ì´ë¦„ ì¤‘ í•˜ë‚˜ì— ì»¤ì„œë¥¼ ë†“ê³  **Ctrl+1** ì„ ëˆŒëŸ¬ **Quick Assist** ë·°ë¥¼ ì—´ê³  _**`Add all 2 missing methods of entity zrap100_r_travel_### ...`**_ í•­ëª©ì„ ì„ íƒí•©ë‹ˆë‹¤.

    ê²°ê³¼ì ìœ¼ë¡œ **`FOR VALIDATE ON SAVE`** ë©”ì„œë“œ **`validateCustomer`** ì™€ **`validateDates`** ê°€ _Travel_ BO ì—”í‹°í‹°ì˜ behavior pool ![class icon](images/adt_class.png)`ZRAP100_BP_TRAVELTP_###`ì˜ ë¡œì»¬ í•¸ë“¤ëŸ¬ í´ë˜ìŠ¤ `lcl_handler`ì— ì¶”ê°€ë©ë‹ˆë‹¤.

    <!-- ![Travel BO Behavior Pool](images/new19.png)  -->
    <img src="images/new19.png" alt="validation" width="90%">

7.  ![save icon](images/adt_save.png) ì €ì¥í•˜ê³  ![activate icon](images/adt_activate.png) ë³€ê²½ì‚¬í•­ì„ í™œì„±í™”í•©ë‹ˆë‹¤.

> íŒíŠ¸:
> behavior implementationì—ì„œ `The entity "ZRAP100_R_TRAVELTP_###" does not have a validation "VALIDATECUSTOMER".` ì˜¤ë¥˜ ë©”ì‹œì§€ê°€ í‘œì‹œë˜ë©´ behavior definitionì„ ë‹¤ì‹œ í•œë²ˆ í™œì„±í™”í•´ ë³´ì‹­ì‹œì˜¤.

</details>

## ì—°ìŠµ 5.2: Validation `validateCustomer` êµ¬í˜„
[^ë§¨ ìœ„ë¡œ](#)

> ì†Œë¹„ìê°€ ì…ë ¥í•œ ê³ ê° ID(`CustomerID`)ê°€ ìœ íš¨í•œì§€ í™•ì¸í•˜ëŠ” `validateCustomer` validationì„ êµ¬í˜„í•©ë‹ˆë‹¤.
> ê° ìœ íš¨í•˜ì§€ ì•Šì€ ê°’ì— ëŒ€í•´ ì ì ˆí•œ ë©”ì‹œì§€ê°€ ë°œìƒí•˜ì—¬ UIì— í‘œì‹œë˜ì–´ì•¼ í•©ë‹ˆë‹¤.

<details>
  <summary>ğŸ”µ í´ë¦­í•˜ì—¬ í¼ì¹˜ê¸°!</summary>

1.  ë¨¼ì €, _Travel_ BO ì—”í‹°í‹°ì˜ behavior pool ![class icon](images/adt_class.png)**`ZRAP100_BP_TRAVELTP_###`** ì˜ ë¡œì»¬ í•¸ë“¤ëŸ¬ í´ë˜ìŠ¤ `lcl_handler`ì˜ ì„ ì–¸ë¶€ì—ì„œ ìƒˆ ë©”ì„œë“œì˜ ì¸í„°í˜ì´ìŠ¤ë¥¼ í™•ì¸í•©ë‹ˆë‹¤.

    ì´ë¥¼ ìœ„í•´ ë©”ì„œë“œ ì´ë¦„ **`validateCustomer`** ì— ì»¤ì„œë¥¼ ë†“ê³  **F2** ë¥¼ ëˆŒëŸ¬ **ABAP Element Info** ë·°ë¥¼ ì—´ê³  ì „ì²´ ë©”ì„œë“œ ì¸í„°í˜ì´ìŠ¤ë¥¼ ê²€ì‚¬í•©ë‹ˆë‹¤.

    ![Travel BO Behavior Pool](images/v3.png)

    ** ê°€ë‹¨í•œ ì„¤ëª…**:
    -   **`FOR VALIDATE ON SAVE`** ì¶”ê°€ êµ¬ë¬¸ì€ ì´ ë©”ì„œë“œê°€ ì €ì¥ ì‹œ ì‹¤í–‰ë˜ëŠ” validationì˜ êµ¬í˜„ì„ ì œê³µí•¨ì„ ë‚˜íƒ€ëƒ…ë‹ˆë‹¤. Validationsì€ í•­ìƒ ì €ì¥ ì‹œ ì‹¤í–‰ë©ë‹ˆë‹¤.
    -   validation ë©”ì„œë“œì˜ ì‹œê·¸ë‹ˆì²˜:
        -   `IMPORTING` ë§¤ê°œë³€ìˆ˜ **`keys`** - validationì´ ìˆ˜í–‰ë˜ì–´ì•¼ í•˜ëŠ” ì¸ìŠ¤í„´ìŠ¤ì˜ í‚¤ë¥¼ í¬í•¨í•˜ëŠ” ë‚´ë¶€ í…Œì´ë¸”.
        -   ì•”ì‹œì  `CHANGING` ë§¤ê°œë³€ìˆ˜ (ì¼ëª… _ì•”ì‹œì  ì‘ë‹µ ë§¤ê°œë³€ìˆ˜_):
            -   **`failed`** - ì˜¤ë¥˜ê°€ ë°œìƒí•œ ë°ì´í„° ì„¸íŠ¸ë¥¼ ì‹ë³„í•˜ê¸° ìœ„í•œ ì •ë³´ê°€ ìˆëŠ” í…Œì´ë¸”
            -   **`reported`** - ì¸ìŠ¤í„´ìŠ¤ë³„ ë©”ì‹œì§€ìš© ë°ì´í„°ê°€ ìˆëŠ” í…Œì´ë¸”

    ì´ì œ validation ë©”ì„œë“œë¥¼ êµ¬í˜„í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

2.  ì´ì œ í´ë˜ìŠ¤ì˜ êµ¬í˜„ë¶€ì—ì„œ **`validateCustomer`** ë©”ì„œë“œë¥¼ êµ¬í˜„í•©ë‹ˆë‹¤.

    ë¡œì§ì€ ë‹¤ìŒê³¼ ê°™ì€ ì£¼ìš” ë‹¨ê³„ë¡œ êµ¬ì„±ë©ë‹ˆë‹¤:
    1.  EML êµ¬ë¬¸ **`READ ENTITIES`** ë¥¼ ì‚¬ìš©í•˜ì—¬ ì „ì†¡ëœ í‚¤(**`keys`**)ì˜ travel ì¸ìŠ¤í„´ìŠ¤ë¥¼ ì½ìŠµë‹ˆë‹¤.
    2.  **`FIELDS`** ì¶”ê°€ êµ¬ë¬¸ì€ ì½ì„ í•„ë“œë¥¼ ì§€ì •í•˜ëŠ” ë° ì‚¬ìš©ë©ë‹ˆë‹¤. í˜„ì¬ validationì—ëŠ” **`CustomerID`** ë§Œ ê´€ë ¨ì´ ìˆìŠµë‹ˆë‹¤.
        `ALL FIELDS` ì¶”ê°€ êµ¬ë¬¸ì„ ì‚¬ìš©í•˜ì—¬ ëª¨ë“  í•„ë“œë¥¼ ì½ì„ ìˆ˜ ìˆìŠµë‹ˆë‹¤.
    3.  **`IN LOCAL MODE`** ì¶”ê°€ êµ¬ë¬¸ì€ ê¸°ëŠ¥ ì œì–´ ë° ê¶Œí•œ ê²€ì‚¬ë¥¼ ì œì™¸í•˜ëŠ” ë° ì‚¬ìš©ë©ë‹ˆë‹¤.
    4.  ì „ì†¡ëœ (ê³ ìœ í•˜ê³ , ì´ˆê¸°ê°’ì´ ì•„ë‹Œ) ëª¨ë“  ê³ ê° IDë¥¼ ì½ê³  ì¡´ì¬í•˜ëŠ”ì§€ í™•ì¸í•©ë‹ˆë‹¤.
    5.  ì´ˆê¸°ê°’ì´ê±°ë‚˜ ì¡´ì¬í•˜ì§€ ì•ŠëŠ” ê³ ê° ID(**`CustomerID`**)ë¥¼ ê°€ì§„ ì „ì†¡ëœ ëª¨ë“  _travel_ ì¸ìŠ¤í„´ìŠ¤ì— ëŒ€í•œ ë©”ì‹œì§€ë¥¼ ì¤€ë¹„/ë°œìƒì‹œí‚¤ê³ 
        changing ë§¤ê°œë³€ìˆ˜ **`reported`** ë¥¼ ì„¤ì •í•©ë‹ˆë‹¤.

    **`validateCustomer`** ì˜ í˜„ì¬ ë©”ì„œë“œ êµ¬í˜„ì„ ë‹¤ìŒ ì½”ë“œ ìŠ¤ë‹ˆí«ìœ¼ë¡œ ë°”ê¾¸ê³  ìë¦¬ í‘œì‹œì **`###`** ì˜ ëª¨ë“  ë°œìƒì„ ê·¸ë£¹ IDë¡œ ë°”ê¿‰ë‹ˆë‹¤.

    **F1 ë„ì›€ë§** ì„ ì‚¬ìš©í•˜ì—¬ ë‹¤ë¥¸ ABAP ë° EML êµ¬ë¬¸ì— ëŒ€í•œ ìì„¸í•œ ì •ë³´ë¥¼ ì–»ì„ ìˆ˜ ìˆìŠµë‹ˆë‹¤.

    ```ABAP
    **********************************************************************
    * Validation: Check the validity of the entered customer data
    **********************************************************************
      METHOD validateCustomer.
        "read relevant travel instance data
        READ ENTITIES OF ZRAP100_R_TravelTP_### IN LOCAL MODE
        ENTITY Travel
         FIELDS ( CustomerID )
         WITH CORRESPONDING #( keys )
        RESULT DATA(travels).

        DATA customers TYPE SORTED TABLE OF /dmo/customer WITH UNIQUE KEY customer_id.

        "optimization of DB select: extract distinct non-initial customer IDs
        customers = CORRESPONDING #( travels DISCARDING DUPLICATES MAPPING customer_id = customerID EXCEPT * ).
        DELETE customers WHERE customer_id IS INITIAL.
        IF customers IS NOT INITIAL.

          "check if customer ID exists
          SELECT FROM /dmo/customer FIELDS customer_id
                                    FOR ALL ENTRIES IN @customers
                                    WHERE customer_id = @customers-customer_id
            INTO TABLE @DATA(valid_customers).
        ENDIF.

        "raise msg for non existing and initial customer id
        LOOP AT travels INTO DATA(travel).

          APPEND VALUE #(  %tky                 = travel-%tky
                           %state_area          = 'VALIDATE_CUSTOMER'
                         ) TO reported-travel.

          IF travel-CustomerID IS  INITIAL.
            APPEND VALUE #( %tky = travel-%tky ) TO failed-travel.

            APPEND VALUE #( %tky                = travel-%tky
                            %state_area         = 'VALIDATE_CUSTOMER'
                            %msg                = NEW /dmo/cm_flight_messages(
                                                                    textid   = /dmo/cm_flight_messages=>enter_customer_id
                                                                    severity = if_abap_behv_message=>severity-error )
                            %element-CustomerID = if_abap_behv=>mk-on
                          ) TO reported-travel.

          ELSEIF travel-CustomerID IS NOT INITIAL AND NOT line_exists( valid_customers[ customer_id = travel-CustomerID ] ).
            APPEND VALUE #(  %tky = travel-%tky ) TO failed-travel.

            APPEND VALUE #(  %tky                = travel-%tky
                             %state_area         = 'VALIDATE_CUSTOMER'
                             %msg                = NEW /dmo/cm_flight_messages(
                                                                    customer_id = travel-customerid
                                                                    textid      = /dmo/cm_flight_messages=>customer_unkown
                                                                    severity    = if_abap_behv_message=>severity-error )
                             %element-CustomerID = if_abap_behv=>mk-on
                          ) TO reported-travel.
          ENDIF.

        ENDLOOP.
      ENDMETHOD.
    ```

3.  ![save icon](images/adt_save.png) ì €ì¥í•˜ê³  ![activate icon](images/adt_activate.png) ë³€ê²½ì‚¬í•­ì„ í™œì„±í™”í•©ë‹ˆë‹¤.

</details>

## ì—°ìŠµ 5.3: Validation `validateDates` êµ¬í˜„
[^ë§¨ ìœ„ë¡œ](#)

> ì…ë ¥ëœ ì‹œì‘ì¼(`BeginDate`)ê³¼ ì¢…ë£Œì¼(`EndDate`)ì˜ ìœ íš¨ì„±ì„ í™•ì¸í•˜ëŠ” `validateDates` validationì„ êµ¬í˜„í•©ë‹ˆë‹¤.
> ê° ìœ íš¨í•˜ì§€ ì•Šì€ ê°’ì— ëŒ€í•´ ì ì ˆí•œ ë©”ì‹œì§€ê°€ ë°œìƒí•˜ì—¬ UIì— í‘œì‹œë˜ì–´ì•¼ í•©ë‹ˆë‹¤.

<details>
  <summary>ğŸ”µ í´ë¦­í•˜ì—¬ í¼ì¹˜ê¸°!</summary>

1.  êµ¬í˜„ í´ë˜ìŠ¤ ![class](images/adt_class.png) **`ZRAP100_BP_TRAVELTP_###`** ì—ì„œ **`validateDates`** ì˜ í˜„ì¬ ë©”ì„œë“œ êµ¬í˜„ì„ ë‹¤ìŒ ì½”ë“œ ìŠ¤ë‹ˆí«ìœ¼ë¡œ ë°”ê¾¸ê³  ìë¦¬ í‘œì‹œì **`###`** ì˜ ëª¨ë“  ë°œìƒì„ ê·¸ë£¹ IDë¡œ ë°”ê¿‰ë‹ˆë‹¤.

    ì£¼ìš” êµ¬í˜„ ë‹¨ê³„ëŠ” **`validateCustomer`** ë©”ì„œë“œì˜ ë‹¨ê³„ì™€ ìœ ì‚¬í•©ë‹ˆë‹¤.

    ì´ validationì€ **`BeginDate`** ë° **`EndDate`** í•„ë“œì—ì„œ ìˆ˜í–‰ë©ë‹ˆë‹¤. ì…ë ¥ëœ ì‹œì‘ì¼(`BeginDate`)ì´ ë¯¸ë˜ì¸ì§€, ì…ë ¥ëœ ì¢…ë£Œì¼(`EndDate`)ì˜ ê°’ì´ ì‹œì‘ì¼(`BeginDate`) ì´í›„ì¸ì§€ í™•ì¸í•©ë‹ˆë‹¤.

    ```ABAP
    **********************************************************************
    * Validation: Check the validity of begin and end dates
    **********************************************************************
      METHOD validateDates.

        READ ENTITIES OF ZRAP100_R_TravelTP_### IN LOCAL MODE
          ENTITY Travel
            FIELDS (  BeginDate EndDate TravelID )
            WITH CORRESPONDING #( keys )
          RESULT DATA(travels).

        LOOP AT travels INTO DATA(travel).

          APPEND VALUE #(  %tky               = travel-%tky
                           %state_area        = 'VALIDATE_DATES' ) TO reported-travel.

          IF travel-BeginDate IS INITIAL.
            APPEND VALUE #( %tky = travel-%tky ) TO failed-travel.

            APPEND VALUE #( %tky               = travel-%tky
                            %state_area        = 'VALIDATE_DATES'
                             %msg              = NEW /dmo/cm_flight_messages(
                                                                    textid   = /dmo/cm_flight_messages=>enter_begin_date
                                                                    severity = if_abap_behv_message=>severity-error )
                            %element-BeginDate = if_abap_behv=>mk-on ) TO reported-travel.
          ENDIF.
          IF travel-BeginDate < cl_abap_context_info=>get_system_date( ) AND travel-BeginDate IS NOT INITIAL.
            APPEND VALUE #( %tky               = travel-%tky ) TO failed-travel.

            APPEND VALUE #( %tky               = travel-%tky
                            %state_area        = 'VALIDATE_DATES'
                             %msg              = NEW /dmo/cm_flight_messages(
                                                                    begin_date = travel-BeginDate
                                                                    textid     = /dmo/cm_flight_messages=>begin_date_on_or_bef_sysdate
                                                                    severity   = if_abap_behv_message=>severity-error )
                            %element-BeginDate = if_abap_behv=>mk-on ) TO reported-travel.
          ENDIF.
          IF travel-EndDate IS INITIAL.
            APPEND VALUE #( %tky = travel-%tky ) TO failed-travel.

            APPEND VALUE #( %tky               = travel-%tky
                            %state_area        = 'VALIDATE_DATES'
                             %msg                = NEW /dmo/cm_flight_messages(
                                                                    textid   = /dmo/cm_flight_messages=>enter_end_date
                                                                    severity = if_abap_behv_message=>severity-error )
                            %element-EndDate   = if_abap_behv=>mk-on ) TO reported-travel.
          ENDIF.
          IF travel-EndDate < travel-BeginDate AND travel-BeginDate IS NOT INITIAL
                                               AND travel-EndDate IS NOT INITIAL.
            APPEND VALUE #( %tky = travel-%tky ) TO failed-travel.

            APPEND VALUE #( %tky               = travel-%tky
                            %state_area        = 'VALIDATE_DATES'
                            %msg               = NEW /dmo/cm_flight_messages(
                                                                    textid     = /dmo/cm_flight_messages=>begin_date_bef_end_date
                                                                    begin_date = travel-BeginDate
                                                                    end_date   = travel-EndDate
                                                                    severity   = if_abap_behv_message=>severity-error )
                            %element-BeginDate = if_abap_behv=>mk-on
                            %element-EndDate   = if_abap_behv=>mk-on ) TO reported-travel.
          ENDIF.
        ENDLOOP.

      ENDMETHOD.
    ```

2.  ![save icon](images/adt_save.png) ì €ì¥í•˜ê³  ![activate icon](images/adt_activate.png) ë³€ê²½ì‚¬í•­ì„ í™œì„±í™”í•©ë‹ˆë‹¤.

</details>

## ì—°ìŠµ 5.4: ê°œì„ ëœ Travel ì•± ë¯¸ë¦¬ë³´ê¸° ë° í…ŒìŠ¤íŠ¸
[^ë§¨ ìœ„ë¡œ](#)

ì´ì œ SAP Fiori elements ì•±ì„ í…ŒìŠ¤íŠ¸í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

<details>
  <summary>ğŸ”µ í´ë¦­í•˜ì—¬ í¼ì¹˜ê¸°!</summary>

ë¸Œë¼ìš°ì €ê°€ ì•„ì§ ì—´ë ¤ ìˆëŠ” ê²½ìš° ë¸Œë¼ìš°ì €ì—ì„œ **F5** ë¥¼ ì‚¬ìš©í•˜ì—¬ ì• í”Œë¦¬ì¼€ì´ì…˜ì„ ìƒˆë¡œ ê³ ì¹˜ê±°ë‚˜, service binding **`ZRAP100_UI_TRAVEL_O4_###`** ë¡œ ì´ë™í•˜ì—¬ **`Travel`** ì—”í‹°í‹° ì„¸íŠ¸ì— ëŒ€í•œ Fiori elements ì•± ë¯¸ë¦¬ë³´ê¸°ë¥¼ ì‹œì‘í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

1.  **Create** ë¥¼ í´ë¦­í•˜ì—¬ ìƒˆ í•­ëª©ì„ ë§Œë“­ë‹ˆë‹¤.

2.  Agency IDë¡œ `Sunshine Travel (70001)`ì„ ì„ íƒí•˜ê³ , ì´ë¦„ `Theresia`ë¥¼ ì…ë ¥í•˜ì—¬ ê³ ê°ì„ ì„ íƒí•˜ê³ , ì‹œì‘ì¼ë¡œ 2022ë…„ 11ì›” 20ì¼, ì¢…ë£Œì¼ë¡œ 2022ë…„ 11ì›” 15ì¼ì„ ì„ íƒí•©ë‹ˆë‹¤. draftê°€ ì—…ë°ì´íŠ¸ë©ë‹ˆë‹¤.

    <!-- ![Preview](images/preview3.png) --Y
    <img src="images/preview3.png" alt="Preview" width="90%">        
     
3. Now click **Create**. You should get following error messages displayed:  
   *Begin Date 11/20/2022 must not be after End Date 11/16/2022* .

    <!-- ![Preview](images/preview4.png)  -->
    <img src="images/preview3.png" alt="Preview" width="90%">

    ![Preview](images/preview5.png)

</details>

## ìš”ì•½
[^ë§¨ ìœ„ë¡œ](#)

ì´ì œ ì—¬ëŸ¬ë¶„ì€...
-   behavior definitionì—ì„œ ë‘ ê°œì˜ validationì„ ì •ì˜í–ˆê³ ,
-   behavior poolì—ì„œ ì´ë¥¼ êµ¬í˜„í–ˆìœ¼ë©°,
-   í–¥ìƒëœ Fiori elements ì•±ì„ ë¯¸ë¦¬ë³´ê³  í…ŒìŠ¤íŠ¸í–ˆìŠµë‹ˆë‹¤.

ë‹¤ìŒ ì—°ìŠµìœ¼ë¡œ ê³„ì† ì§„í–‰í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤ â€“ **[ì—°ìŠµë¬¸ì œ 6: BO ê¸°ëŠ¥ ê°œì„  â€“ Actions](../ex06/README.md)**

---
